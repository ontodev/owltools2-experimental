# # Multiple Labels
#
# Problem: An entity has more than one label. This may cause confusion or misuse, and will prevent translation to OBO format. Excludes deprecated entities. Allows for multiple labels with different language tags, assuming that if there is no language tag, it is an English label.
#
# Solution: Remove extra labels.

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?entity ?property ?value WHERE {
  VALUES ?property { rdfs:label }
  ?entity ?property ?value .
  ?entity ?property ?value2 .
  FILTER (?value != ?value2) .
  FILTER (lang(?value) = lang(?value2) ||
  	((lang(?value) = "") && (lang(?value2) = "en")) ||
  	((lang(?value) = "en") && (lang(?value2) = "")))
  FILTER NOT EXISTS { ?entity owl:deprecated true }
  FILTER (!isBlank(?entity))
}
ORDER BY ?entity
